#!/usr/bin/awk -f
#
#  pslQueryUniq [-m mod] [psl1 ...]
#
# make PSL qNames unique.  This always adds something to the qName, so it's
# always and something and is consistent if run multiple times on the same
# data.  First time it's run, it adds a -n (acc-1, acc-2), subsequent
# runs add another period and number.  (acc-1.1, acc-1.2, acc-2.1)
# If -m is specified, modNum is added (acc-1.tm1)
BEGIN {
    FS = "\t";
    OFS = "\t";
    if (ARGV[1] == "-m") {
        mod = ARGV[2];
        ARGV[1] = ARGV[2] = "";
    }
}

{
    uacc = $10;
    qNameCnt[uacc]++;
    
    if (match(uacc, "-[.0-9]+") == 0) {
        # first added id
        uacc2 = mod uacc "-" qNameCnt[uacc];
    } else {
        # subsequent added ids
        uacc2 = mod uacc "." qNameCnt[uacc];
    }
    $10 = uacc2;
    print $0;
}
